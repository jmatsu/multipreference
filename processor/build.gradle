buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}


apply plugin: 'kotlin'

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.novoda.bintray-release'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceSets {
    main.java.srcDirs += project(":library").file("src/main/java")
    test.java.srcDirs += project(":library").file("src/test/java")
}

def androidJarLocation() {
    def sdkDir = System.getenv("ANDROID_HOME")

    if (sdkDir != null) {
        return "$sdkDir/platforms/android-${config.compileSdkVersion}/android.jar"
    }

    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        return "${properties.getProperty('sdk.dir')}/platforms/android-${config.compileSdkVersion}/android.jar"
    }

    throw new AssertionError("Need to set ANDROID_HOME or prepare local.properties")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.funktionale:funktionale-either:$funktionaleVersion"

    implementation "com.android.support:support-annotations:$supportLibraryVersion"

    implementation "com.squareup:javapoet:$javapoetVersion"
    implementation "com.google.guava:guava:$guavaVersion"

    compileOnly files(androidJarLocation())
    runtimeOnly "com.google.android:android:$androidVersion"

    testImplementation "org.jetbrains.spek:spek-api:$spekVersion"
    testImplementation "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"

    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
}

publish {
    def versionFile = new File(rootDir, 'VERSION')

    userOrg = artifactConfig.user
    groupId = artifactConfig.groupId
    artifactId = artifactConfig.processorArtifactId

    version = (versionFile.exists() ? versionFile.text : releaseVersionCandidate).trim()
    
    desc = artifactConfig.description
    website = artifactConfig.websiteUrl
    licences = artifactConfig.licenses
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}